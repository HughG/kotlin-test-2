group 'org.tameter.partial-order'
version '0.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
    jcenter()
    mavenLocal() // For local install of unpublished Yested versions.
}

dependencies {
    //Fill this in with the version of kotlinx in use in your project
    def kotlinx_html_version = "0.6.3"

    def yested_version = '1.0-SNAPSHOT'

    // include for server side
    // compile "org.jetbrains.kotlinx:kotlinx-html-jvm:${kotlinx_html_version}"

    // include for client-side
    compile "org.jetbrains.kotlinx:kotlinx-html-js:${kotlinx_html_version}"

    // note: most likely you don't need to include them both
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"

    // local maven-installed Yested_fw
    compile "net.yested:Yested:$yested_version"
}

compileKotlin2Js {
    kotlinOptions.sourceMap = true
}

task deployJs(type: Copy) {
    group = 'deploy'
    dependsOn build

    includeEmptyDirs = false

    configurations.runtime.each {
        from zipTree(it.absolutePath)
    }
    configurations.runtime.allArtifacts.each { PublishArtifact artifact ->
        from zipTree(artifact.file.absolutePath)
    }
    into "${projectDir}/web/js"
    include { fileTreeElement ->
        def path = fileTreeElement.path
        (path.endsWith(".js") || path.endsWith(".js.map")) &&
            (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
    }
}

task deployHtml(type: Copy) {
    group = 'deploy'
    dependsOn build

    includeEmptyDirs = false

    configurations.runtime.each {
        from zipTree(it.absolutePath)
    }
    configurations.runtime.allArtifacts.each { PublishArtifact artifact ->
        from zipTree(artifact.file.absolutePath)
    }
    into "${projectDir}/web"
    include { fileTreeElement ->
        def path = fileTreeElement.path
        (path.endsWith(".html") || path.endsWith(".css")) &&
            (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
    }
}

task deploy {
    group = 'deploy'
    dependsOn deployJs, deployHtml
}